pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Controls
import qs.Commons
import "Chart.js" as Chart
import "Figures.js" as Figures
import "Settings.js" as Settings
import "Tones.js" as Tones
// The scrolling rows. Owns identity, drag, cursor, scroll-to-row, and the
// wheel. Surfaces pass data in and persist the signals. The rows stop short
// of the right edge by one gutter, which is where the scrollbar lives, so a
// thumb never sits on a row. There is one row for every symbol in All, kept
// across lists: a list shows its members and hides the rest, so a switch
// builds nothing.
Flickable {
  id: root

  // The service's view: the list and its key, its order and direction, its
  // rows, those rows sorted, and All. It arrives whole, so one comparison
  // with the last says what changed.
  // Handlers read it, never a binding derived from it, which may not have
  // caught up when they run.
  property var view: ({ list: "", listKey: "=", order: "manual", reversed: false, rows: [], shown: [], library: [] })
  property var quotes: ({})
  property var entries: ({})
  property string featuredSymbol: ""
  property int scrubShown: 0
  property string changeMode: "pct"
  property int now: 0
  property var asked: ({})
  property bool retro: false
  // The exchanges' calendars, for the breaks in a row's day line.
  property var calendars: null
  property color upColor: Color.foreground
  property color downColor: Color.urgent
  property color foreground: Color.foreground
  property color dim: Tones.dim(foreground, Color.background)
  property color dimmer: Tones.dimmer(foreground, Color.background)
  property string fontFamily: Style.font.family
  property int rowHeight: Style.space(40)
  property int rowGap: Style.space(2)
  property int gutter: Style.space(8)
  // A closed or closing surface: a pointer moving over it moves no cursor.
  property bool surfaceOpen: true
  // The card's own colour, which a moving row carries under its fill.
  property color ground: "transparent"

  property string cursorSymbol: ""
  property var displayedSymbols: []
  property point pointerScenePosition: Qt.point(-1, -1)
  property string dragSymbol: ""
  property real dragY: 0
  property int dragTarget: -1
  property real dragOffset: 0
  // The view the rows were last arranged for.
  property var arrangedView: null

  readonly property int rowPitch: rowHeight + rowGap
  // Where the list is headed: a glide's destination, or where it rests.
  readonly property real headedY: glide.running ? glide.to : contentY
  // The row the keys act on, always one in sight, so a key never acts on a
  // row you cannot see. A cursor of your own stays while it is in sight;
  // without one (on open, after a drag, once the pointer moves) the cursor
  // sits on the featured row when that is in sight, else on the first whole
  // row in sight. Read where the list is headed, so a key gliding the list
  // to its row keeps it.
  readonly property string cursorRow: {
    var top = headedY
    if (ownInSight(top)) return cursorSymbol
    var hero = displayedSymbols.indexOf(featuredSymbol)
    if (hero >= 0 && wholeInSight(hero, top)) return featuredSymbol
    var first = Math.max(0, Math.ceil((top - 0.5) / rowPitch))
    return displayedSymbols[Math.min(first, displayedSymbols.length - 1)] || ""
  }
  // A cursor of your own that leaves sight, as the wheel scrolls, the list
  // shortens, or the rows change under it, is let go: the cursor moves by
  // the rule above and stays there when the row comes back. Let go as the
  // sight changes, not as cursorRow does: it reads the cursor it would clear.
  onHeadedYChanged: letGoUnseen()
  onHeightChanged: letGoUnseen()
  onDisplayedSymbolsChanged: letGoUnseen()
  function ownInSight(top) {
    var own = displayedSymbols.indexOf(cursorSymbol)
    return cursorSymbol !== "" && own >= 0 && wholeInSight(own, top)
  }
  function letGoUnseen() {
    if (cursorSymbol !== "" && !ownInSight(headedY)) cursorSymbol = ""
  }
  readonly property bool sortHeld: listHover.hovered || dragSymbol !== ""
  readonly property bool manualOrder: view.order === "manual"

  signal featureRequested(string symbol)
  signal removeRequested(string symbol)
  signal listsRequested(string symbol)
  signal manualOrderRequested(var symbols)
  // The rows show the latest view.
  signal arranged()
  // A row's slide or fade has ended.
  signal rowRested(string symbol)

  implicitHeight: Math.max(0, displayedSymbols.length * rowPitch - rowGap)
  contentWidth: width
  contentHeight: implicitHeight
  clip: true
  boundsBehavior: Flickable.StopAtBounds
  flickableDirection: Flickable.VerticalFlick
  interactive: contentHeight > height
  // A drag or a flick takes the list over from the wheel, and settles it
  // the way it always has.
  onMovementStarted: takeOver()
  onMovementEnded: snapToRow()
  ScrollBar.vertical: ScrollBar {
    policy: ScrollBar.AsNeeded
    visible: root.interactive
    active: root.interactive
    onPressedChanged: if (!pressed) root.snapToRow()
  }

  // Touchpad scrolling reaches this handler too (its events are marked as
  // system-synthesized, which a WheelHandler ignores unless it accepts the
  // touchpad), so the finger and the wheel settle through the same glide.
  WheelHandler {
    acceptedModifiers: Qt.NoModifier
    acceptedDevices: PointerDevice.Mouse | PointerDevice.TouchPad
    onWheel: function(event) {
      // A touchpad reports the pixels the finger moved: the list follows
      // them exactly, and settles on a row once the finger stops.
      if (event.pixelDelta.y !== 0) root.followBy(-event.pixelDelta.y)
      // A notch is 120 units and one row; a high-resolution mouse sends many
      // smaller ticks, and each moves its fraction of a row.
      else if (event.angleDelta.y !== 0) root.wheelBy(-event.angleDelta.y / 120)
      else event.accepted = false
    }
  }

  // The wheel glides the list rather than setting it, so a stream of small
  // ticks reads as motion instead of steps, and settles on a row boundary
  // once the wheel goes quiet. Snapping every tick is what made it lurch.
  NumberAnimation {
    id: glide
    target: root
    property: "contentY"
    duration: 160
    easing.type: Easing.OutCubic
  }

  // Comfortably past the glide, so a stream of ticks never snaps mid-scroll.
  Timer {
    id: wheelQuiet
    interval: 260
    onTriggered: root.glideTo(root.snappedY(root.wheelTarget()))
  }

  HoverHandler {
    id: listHover
    parent: root
    onPointChanged: {
      var next = point.scenePosition
      var moved = next.x !== root.pointerScenePosition.x || next.y !== root.pointerScenePosition.y
      root.pointerScenePosition = next
      if (hovered && moved && root.surfaceOpen) {
        root.cursorSymbol = ""
        root.wheelMoving = ""
      }
    }
  }

  function entryOf(symbol) {
    return entries && entries[symbol] ? entries[symbol] : null
  }

  function freshnessOf(symbol) {
    return Figures.freshness(entryOf(symbol), quotes[symbol] || null, now, asked[symbol] || 0)
  }

  function moveCursor(delta) {
    if (displayedSymbols.length === 0) return
    var current = displayedSymbols.indexOf(cursorRow)
    var next = Math.max(0, Math.min(displayedSymbols.length - 1, current < 0 ? 0 : current + delta))
    select(displayedSymbols[next])
  }

  function clampY(y) {
    return Math.max(0, Math.min(Math.max(0, contentHeight - height), y))
  }

  function snappedY(y) {
    return clampY(Math.round(y / rowPitch) * rowPitch)
  }

  function snapToRow() {
    takeOver()
    contentY = snappedY(contentY)
  }

  // True while the list is still on its way to where it will rest: a glide,
  // the wheel's settle still to come, or a flick. A glide the wheel stops and
  // restarts in one turn passes through false, so a reader waits a turn.
  readonly property bool settling: glide.running || wheelQuiet.running || moving

  // Something other than the wheel now owns the list's position: a lifted
  // row, the keyboard, a flick, the scrollbar. The wheel's glide and its
  // pending settle both stop, so neither moves the list underneath it.
  function takeOver() {
    glide.stop()
    wheelQuiet.stop()
  }

  // Where the wheel has the list headed: its glide's destination, or where
  // it is resting.
  function wheelTarget() {
    return glide.running ? glide.to : contentY
  }

  function wheelBy(rows) {
    glideTo(wheelTarget() + rows * rowPitch)
    wheelQuiet.restart()
  }

  // The touchpad's pixels move the list directly; the same quiet timer then
  // glides it onto a row.
  function followBy(pixels) {
    glide.stop()
    contentY = clampY(contentY + pixels)
    wheelQuiet.restart()
  }

  function glideTo(y) {
    var target = clampY(y)
    glide.stop()
    if (target === contentY) return
    glide.from = contentY
    glide.to = target
    glide.start()
  }

  // The list glides, as the wheel glides it, to rest on a row boundary with
  // row `index` whole in view: never a jump, and never left between rows.
  // Rows moving at the same time slide with it, so none jumps on screen.
  function scrollToRow(index) {
    wheelQuiet.stop()
    var y = index * rowPitch
    var at = wheelTarget()
    if (y < at) at = y
    else if (y + rowHeight > at + height) at = y + rowHeight - height
    glideTo(snappedY(at))
  }

  // The cursor on a shown row, carried into view.
  // The list heads for the row first, so the cursor is set on a row in
  // sight.
  function select(symbol) {
    var index = displayedSymbols.indexOf(symbol)
    if (index < 0) return
    scrollToRow(index)
    cursorSymbol = symbol
  }

  // Whether row `index` is whole in the list's view with it scrolled to
  // `top`. The card's edge easing past the foot is left out: the cursor
  // follows the scroll, not a moment of the card's motion.
  function wholeInSight(index, top) {
    var y = index * rowPitch
    return y >= top - 0.5 && y + rowHeight <= top + height + 0.5
  }

  // Shift and the wheel move a row a place a notch; a high-resolution wheel's
  // small ticks add up to notches. The row the wheel started on keeps moving,
  // whichever row slides under the still pointer, until the pointer moves.
  property string wheelMoving: ""
  property real wheelMoveRemainder: 0
  function wheelMove(symbol, angle) {
    if (wheelMoving === "" || displayedSymbols.indexOf(wheelMoving) < 0) {
      wheelMoving = symbol
      wheelMoveRemainder = 0
    }
    var notches = Util.wheelSteps(wheelMoveRemainder, angle)
    wheelMoveRemainder = notches.remainder
    if (notches.steps !== 0) moveSymbol(wheelMoving, -notches.steps)
  }

  // Shows `next`. Rows never draw through each other: when `animate`, a
  // symbol new to the shown rows joins, fading in at its place, and rows
  // already shown slide (see the delegate). A list that gets shorter at its
  // end, scrolled to it, has its scroll pulled back within its new length;
  // the rows keep their places on screen through that, and the ones above
  // the gap slide down to close it.
  function display(next, animate) {
    var before = displayedSymbols
    var scrolled = contentY
    displayedSymbols = next
    contentY = clampY(contentY)
    if (glide.running && glide.to > clampY(glide.to)) glideTo(glide.to)
    if (!animate || !visible) return
    for (var i = 0; i < next.length; i++) {
      var row = before.indexOf(next[i]) < 0 ? rowItem(next[i]) : null
      if (row) row.join()
    }
    var pulled = scrolled - contentY
    if (pulled <= 0) return
    for (var j = 0; j < next.length; j++) {
      var shownRow = rowItem(next[j])
      if (shownRow) shownRow.carry(pulled)
    }
  }

  // Closing stops each row's slide and fade where they are, so the surface
  // fades out as it was; settleRows puts them at rest before it opens again.
  function pauseRows() {
    for (var i = 0; i < rowRepeater.count; i++) {
      var row = rowRepeater.itemAt(i)
      if (row) row.pause()
    }
  }

  // A slide or fade still running when a new arrangement lands ends where
  // it was going, before anything is drawn.
  function settleRows() {
    for (var i = 0; i < rowRepeater.count; i++) {
      var row = rowRepeater.itemAt(i)
      if (row) row.settle()
    }
  }

  function forgetGoneCursor() {
    if (cursorSymbol && view.rows.indexOf(cursorSymbol) < 0) cursorSymbol = ""
  }

  function applyShown() {
    display(view.shown.slice(), true)
    forgetGoneCursor()
  }

  // Holding freezes positions, not membership: a symbol that leaves goes at
  // once, and one whose place is new, added or brought back, takes its
  // sorted place and joins there, so it stays put when the hold ends.
  function applyMembership() {
    var next = displayedSymbols.filter(function(s) { return view.rows.indexOf(s) >= 0 })
    var joining = view.shown.filter(function(s) { return next.indexOf(s) < 0 })
    for (var i = 0; i < joining.length; i++)
      next.splice(Math.min(view.shown.indexOf(joining[i]), next.length), 0, joining[i])
    display(next, true)
    forgetGoneCursor()
  }

  function commitManualOrder(next) {
    if (!manualOrder) return
    displayedSymbols = next.slice()
    manualOrderRequested(next)
  }

  function moveSymbol(symbol, delta) {
    if (!manualOrder) return
    var from = displayedSymbols.indexOf(symbol)
    var to = from + delta
    if (from < 0 || to < 0 || to >= displayedSymbols.length) return
    commitManualOrder(Settings.movedSymbols(displayedSymbols, from, to))
    scrollToRow(to)
    cursorSymbol = symbol
  }

  function moveSelected(delta) {
    if (cursorRow) moveSymbol(cursorRow, delta)
  }

  function rowItem(symbol) {
    for (var i = 0; i < rowRepeater.count; i++) {
      var item = rowRepeater.itemAt(i) as WatchlistRow
      if (item && item.symbol === symbol) return item
    }
    return null
  }

  // Whether the row shown for `symbol` is still: not sliding, fading, or held.
  function rowResting(symbol) {
    var row = rowItem(symbol)
    return !!row && row.place >= 0 && row.resting
  }

  // Rows use the held projection while the pointer is in the list.
  function slotOf(symbol) {
    var i = displayedSymbols.indexOf(symbol)
    if (dragSymbol === "") return i
    var from = displayedSymbols.indexOf(dragSymbol)
    if (from < dragTarget && i > from && i <= dragTarget) return i - 1
    if (dragTarget < from && i >= dragTarget && i < from) return i + 1
    return i
  }

  // The pointer's height in the view while a row is lifted, so the row
  // stays under it when the wheel scrolls the list beneath.
  property real dragPointer: 0
  onContentYChanged: if (dragSymbol !== "" && !dropAnim.running) dragTo(dragPointer + contentY)

  function beginDrag(symbol, y) {
    if (!manualOrder) return
    var from = displayedSymbols.indexOf(symbol)
    if (from < 0) return
    takeOver()
    dropAnim.stop()
    cursorSymbol = ""
    dragOffset = y - from * rowPitch
    dragPointer = y - contentY
    dragY = from * rowPitch
    dragTarget = from
    dragSymbol = symbol
  }

  function dragTo(y) {
    if (dragSymbol === "") return
    dragPointer = y - contentY
    dragY = Math.max(0, Math.min((displayedSymbols.length - 1) * rowPitch, y - dragOffset))
    dragTarget = Math.round(dragY / rowPitch)
  }

  function endDrag() {
    var from = displayedSymbols.indexOf(dragSymbol)
    var to = dragTarget
    if (from < 0 || to < 0) { cancelDrag(); return }
    if (from !== to) root.commitManualOrder(Settings.movedSymbols(displayedSymbols, from, to))
    dropAnim.to = to * rowPitch
    dropAnim.start()
  }

  // The drag is over; the list, which the lifted row took from the wheel,
  // settles on a row.
  function cancelDrag() {
    dropAnim.stop()
    if (dragSymbol === "") return
    dragSymbol = ""
    glideTo(snappedY(contentY))
  }

  // Closing mid-drag: the drag ends and every row, the lifted one too, stays
  // where it is on screen, still, until the next open settles them.
  function releaseDragInPlace() {
    if (dragSymbol === "") return
    var rows = []
    for (var i = 0; i < rowRepeater.count; i++) {
      var row = rowRepeater.itemAt(i)
      if (row) rows.push({ row: row, y: row.y })
    }
    dropAnim.stop()
    dragSymbol = ""
    rows.forEach(function(r) { r.row.holdAt(r.y) })
  }

  // Another list, another order, or an opening surface is a new
  // arrangement, not a resort under the pointer: thirty rows re-ranking at
  // once explain nothing, so they take their places at once. Another list
  // goes back to where it was left (`places`), the top on its first visit;
  // the same list in a new order keeps its scroll.
  function layOut() {
    display(view.shown.slice(), false)
    settleRows()
    forgetGoneCursor()
  }

  // Where each list was scrolled to when this surface last left it, by the
  // list's key (`Service.listKey`), for the session: a renamed list keeps
  // its place, and a new list under a freed name starts at the top.
  property var places: ({})

  ListModel { id: rowsModel }
  onViewChanged: {
    var before = arrangedView
    arrangedView = view
    if (!before) return
    if (before.library.join() !== view.library.join()) syncRows()
    // A rename keeps the list's key; only another list is a switch. A
    // closed surface's view does not change (StonksBody holds it), so this
    // runs only while it is open, or as it opens, before the open's reopen().
    var switched = view.listKey !== before.listKey
    if (switched) {
      places[before.listKey] = snappedY(wheelTarget())
      cancelDrag()
      cursorSymbol = ""
      takeOver()
      layOut()
      contentY = clampY(places[view.listKey] || 0)
    } else if (view.order !== before.order || view.reversed !== before.reversed) {
      // A row held mid-drag has no manual place to drop into any more.
      cancelDrag()
      layOut()
    } else if (sortHeld) {
      applyMembership()
    } else {
      applyShown()
    }
    arranged()
  }
  onSortHeldChanged: if (!sortHeld && surfaceOpen) applyShown()

  // As the surface opens: the rows in the service's order, at rest, and no
  // Shift+wheel move left half made.
  function reopen() {
    wheelMoving = ""
    layOut()
  }
  Component.onCompleted: {
    arrangedView = view
    syncRows()
    display(view.shown.slice(), false)
  }

  function syncRows() {
    var library = view.library
    for (var i = rowsModel.count - 1; i >= 0; i--) {
      if (library.indexOf(rowsModel.get(i).symbol) < 0) rowsModel.remove(i)
    }
    for (var j = 0; j < library.length; j++) {
      var found = false
      for (var k = 0; k < rowsModel.count; k++) if (rowsModel.get(k).symbol === library[j]) found = true
      if (!found) rowsModel.append({ symbol: library[j] })
    }
  }

  NumberAnimation {
    id: dropAnim
    target: root
    property: "dragY"
    duration: 160
    easing.type: Easing.OutCubic
    onFinished: root.cancelDrag()
  }

  Item {
    id: rowsBox
    width: Math.max(0, root.width - root.gutter)
    height: root.implicitHeight

    Repeater {
      id: rowRepeater
      model: rowsModel

      WatchlistRow {
        id: rowDelegate
        calendars: root.calendars
        readonly property int place: root.slotOf(symbol)
        // A symbol outside this list keeps its row, hidden.
        visible: place >= 0
        y: lifted ? root.dragY : place * root.rowPitch + shift
        // A row slides only if it was in sight before and after the change,
        // so one that joins starts at its own place: it had no place before
        // (`lastPlace` -1), and a row in a hidden list is not in sight now.
        // It slides by easing `shift`, how far it still is from its place, to
        // zero, so a new change mid-slide carries on from where the row is. A
        // moving row carries the card's ground; the one travelling farther
        // rides on top, and the cursor's row wins a tie, so no two rows print
        // through.
        property int lastPlace: -1
        property real shift: 0
        property int travel: 0
        onPlaceChanged: {
          var from = lastPlace
          lastPlace = place
          if (lifted || !visible || from < 0 || place < 0) {
            settle()
            return
          }
          travel = Math.abs(place - from)
          shift += (from - place) * root.rowPitch
          slide.restart()
        }
        NumberAnimation { id: slide; target: rowDelegate; property: "shift"; to: 0; duration: 160; easing.type: Easing.OutCubic }
        moving: lifted || slide.running
        ground: root.ground
        z: lifted ? rowRepeater.count + 2 : (slide.running ? 1 + travel + (cursor ? 0.5 : 0) : 0)
        readonly property bool resting: !slide.running && !fadeIn.running && !lifted
        onRestingChanged: if (resting) root.rowRested(symbol)
        function settle() {
          slide.stop()
          shift = 0
          travel = 0
          fadeIn.stop()
          opacity = 1
        }
        function pause() {
          if (slide.running) slide.pause()
          if (fadeIn.running) fadeIn.pause()
        }
        // Stays at `atY` on screen, still: the slide it would start ends here.
        function holdAt(atY) {
          if (place < 0) return
          slide.stop()
          shift = atY - place * root.rowPitch
        }
        function join() {
          opacity = 0
          fadeIn.restart()
        }
        // Keeps the row where it is on screen while the list's scroll moves
        // by `distance` under it, then slides it to its place.
        function carry(distance) {
          shift -= distance
          if (Math.abs(shift) < 0.5) {
            slide.stop()
            shift = 0
            travel = 0
            return
          }
          travel = Math.max(travel, Math.round(Math.abs(shift) / root.rowPitch))
          slide.restart()
        }
        NumberAnimation { id: fadeIn; target: rowDelegate; property: "opacity"; to: 1; duration: 160; easing.type: Easing.OutCubic }
        width: parent.width
        // A row outside this list rests: no figures derived, nothing drawn.
        // Shown again, it takes its quote and draws before that frame.
        quote: place >= 0 ? root.quotes[symbol] || null : null
        view: quote ? Figures.rowModel(quote, root.scrubShown, root.changeMode) : null
        status: root.entryOf(symbol) ? root.entryOf(symbol).status : "loading"
        freshness: place >= 0 ? root.freshnessOf(symbol) : null
        featuredRow: symbol === root.featuredSymbol
        cursor: symbol === root.cursorRow
        lifted: symbol === root.dragSymbol
        movable: root.manualOrder
        retro: root.retro
        scrubX: quote && root.scrubShown ? Chart.fractionAtTime(quote, root.scrubShown) : -1
        foreground: root.foreground
        dim: root.dim
        dimmer: root.dimmer
        upColor: root.upColor
        downColor: root.downColor
        fontFamily: root.fontFamily
        onFeatureRequested: root.featureRequested(symbol)
        onRemoveRequested: root.removeRequested(symbol)
        onListsRequested: root.listsRequested(symbol)
        onMoveWheeled: function(angle) { root.wheelMove(symbol, angle) }
        onDragStarted: function(y) { root.beginDrag(symbol, y) }
        onDragMoved: function(y) { root.dragTo(y) }
        onDragEnded: root.endDrag()
        onDragCanceled: root.cancelDrag()
      }
    }
  }
}
