import QtQuick
import QtTest
import Quickshell
import qs.Commons
import qs.Ui
import "plugin" as Stonks

// Every key the key sheet draws and the README's table lists acts, on the
// surface it is listed for: pressed with real Qt keys through the real popup
// (Panel.qml, its card in CardWindow.qml) and the real window (App.qml), it
// changes what that surface shows. A key a handler lost, or one renamed in
// the sheet or the README but not in the handlers, fails here. And the sheet
// is the same on both surfaces, and whole in the popup in both looks. HOME
// and curl are scratch fixtures.
ShellRoot {
  id: harness

  property int failures: 0
  // Tab's requests to the bar for its next panel.
  property int tabs: 0

  function check(label, ok, detail) {
    console.log((ok ? "PASS " : "FAIL ") + label + (!ok && detail !== undefined ? " — " + detail : ""))
    if (!ok) failures++
  }

  function find(item, name) {
    if (!item) return null
    if (item.objectName === name) return item
    for (var i = 0; i < item.children.length; i++) {
      var found = find(item.children[i], name)
      if (found) return found
    }
    return item.contentItem ? find(item.contentItem, name) : null
  }

  // The texts an item draws, in order, those with `name` only when given.
  function texts(item, name) {
    var out = []
    var walk = function(node) {
      if (!node) return
      if (node.visible === false) return
      if (node.text !== undefined && (!name || node.objectName === name)) out.push(String(node.text))
      for (var i = 0; i < node.children.length; i++) walk(node.children[i])
    }
    walk(item)
    return out
  }

  function finish() {
    console.log("KEYS DONE")
    done.exitCode = failures ? 1 : 0
    done.start()
  }

  readonly property string readmePath: String(Qt.resolvedUrl("README.md")).replace(/^file:\/\//, "")
  function readFile(path) {
    var view = Qt.createQmlObject('import Quickshell.Io; FileView { blockLoading: true; printErrors: false }', harness)
    view.path = path
    var text = String(view.text())
    view.destroy()
    return text
  }

  // The keys a label names: "1–9" and "`1` to `9`" each digit between, "J K"
  // and "`Shift+J`" alike as Shift+J.
  function tokens(label) {
    var range = /^`?(\d)`?\s*(?:–|to)\s*`?(\d)`?$/.exec(label)
    if (range) {
      var digits = []
      for (var d = Number(range[1]); d <= Number(range[2]); d++) digits.push(String(d))
      return digits
    }
    return label.replace(/`/g, "").split(/\s+/)
      .filter(function(t) { return t !== "" && t !== "or" })
      .map(function(t) { return /^[A-Z]$/.test(t) ? "Shift+" + t : t })
  }

  // The keys the README's table lists for `surface`: a row ending "(popup)"
  // or "(window)" is that surface's alone. Null with no table.
  function readmeKeys(surface) {
    var lines = readFile(readmePath).split("\n")
    var start = lines.indexOf("| Key | Does |")
    if (start < 0) return null
    var keys = []
    for (var i = start + 2; i < lines.length && lines[i].charAt(0) === "|"; i++) {
      var cells = lines[i].split("|")
      var only = /\((popup|window)\)$/.exec(cells[2].trim())
      if (!only || only[1] === surface) keys = keys.concat(tokens(cells[1].trim()))
    }
    return keys
  }

  Stonks.Service { id: service }
  Stonks.Updates { id: updates }
  QtObject {
    id: shellApi
    function serviceFor(id) { return { service: service } }
  }
  PluginBarApi {
    id: api
    pluginId: "grvc.stonks"
    moduleName: "grvc.stonks"
    shell: shellApi
    foreground: Color.foreground
    fontFamily: Style.font.family
    _switchPanelFrom: function(owner, direction) { harness.tabs++; return true }
  }
  Stonks.Panel { id: panel; bar: api; settings: ({}) }
  Stonks.App { id: app; service: service; updates: updates }

  readonly property var popup: ({
    name: "popup",
    body: panel.testBody,
    keys: panel.testKeyCatcher,
    open: function() { panel.open() }
  })
  readonly property var window: ({
    name: "window",
    body: app.testBody,
    keys: app.testKeyCatcher,
    open: function() { app.open("{}") }
  })

  TestCase {
    name: "Keys"
    when: service.pluginsReady

    function quiet() {
      tryVerify(function() { return service.arriving.length === 0 && service.testFeed.firstRun.length + service.testFeed.refreshRun.length === 0 }, 8000)
    }

    // What a surface shows that a key can change.
    function shown(s) {
      var b = s.body
      return JSON.stringify([b.surfaceOpen, b.watchlist.cursorRow, service.featuredSymbol, b.scrubT, b.motion.replayRunning,
        service.range, b.adding, b.listMenuOpen, b.managingLists, b.listsSymbol, service.order, service.reversed,
        service.listName, service.symbols.join(","), service.retro, service.changeMode, b.showingHelp,
        service.testRefreshes, harness.tabs])
    }

    // Every key starts from here: the surface open on All, manual order, the
    // day, nothing open over the rows, the last of the ten rows featured, so
    // each of 1 to 9 has another to feature, and the cursor on the second.
    function base(s) {
      var b = s.body
      if (!b.surfaceOpen) s.open()
      b.undoRemoval()
      b.cancelAdding()
      b.closeListMenu()
      b.closeListViews()
      b.showingHelp = false
      b.endScrub()
      if (service.listName !== "") service.switchList("")
      if (service.order !== "manual") service.setOrder("manual")
      if (service.range !== "1D") service.setRange("1D")
      var rows = b.watchlist.displayedSymbols
      service.feature(rows[rows.length - 1])
      b.watchlist.cursorSymbol = rows[1]
      wait(50)
      s.keys.forceActiveFocus()
    }

    // What a key needs to have something to do, past the base.
    readonly property var setups: ({
      "→": function(s) { s.body.nudgeScrub(-2) },
      "l": function(s) { s.body.nudgeScrub(-2) },
      "u": function(s) { s.body.removeRow(s.body.watchlist.cursorRow) },
      "Shift+O": function(s) { service.setOrder("pct") },
      // The day is the first range.
      "[": function(s) { service.setRange("1W") }
    })

    // One key, as a person presses it; false for a key it can't name.
    function press(token) {
      var named = { "↑": Qt.Key_Up, "↓": Qt.Key_Down, "←": Qt.Key_Left, "→": Qt.Key_Right, "⏎": Qt.Key_Return,
        "Space": Qt.Key_Space, "Delete": Qt.Key_Delete, "Backspace": Qt.Key_Backspace, "Tab": Qt.Key_Tab,
        "Esc": Qt.Key_Escape }
      if (named[token] !== undefined) keyClick(named[token])
      else if (token === "Shift+Tab") keyClick(Qt.Key_Backtab, Qt.ShiftModifier)
      else if (/^Shift\+[A-Z]$/.test(token)) keyClick(token.charAt(6), Qt.ShiftModifier)
      else if (token.length === 1) keyClick(token)
      else return false
      return true
    }

    function acts(s, token) {
      base(s)
      if (setups[token]) setups[token](s)
      wait(50)
      var before = shown(s)
      if (!press(token)) return false
      wait(100)
      return shown(s) !== before
    }

    // The sheet as drawn on `s`, opened with ? and closed again.
    function drawnSheet(s) {
      base(s)
      press("?")
      wait(50)
      var sheet = harness.find(s.body, "helpSheet")
      var drawn = { keys: harness.texts(sheet, "helpKey"), all: harness.texts(sheet), whole: !sheet.interactive }
      press("?")
      return drawn
    }

    function walk(s) {
      var sheet = drawnSheet(s)
      var readme = harness.readmeKeys(s.name)
      var listed = []
      sheet.keys.map(harness.tokens).concat(readme ? [readme] : []).forEach(function(group) {
        group.forEach(function(t) { if (listed.indexOf(t) < 0) listed.push(t) })
      })
      var dead = listed.filter(function(t) { return !acts(s, t) })
      harness.check("in the " + s.name + ", every key the sheet and the README's table list acts (" + listed.length + " keys)",
        readme !== null && dead.length === 0, readme === null ? "no key table in the README" : "dead: " + dead.join(" "))
      return sheet
    }

    function test_keys() {
      // Ten rows, so 9 has a row, and a second list for , and .
      for (var n = 1; n <= 7; n++) service.addSymbol("LONG" + n)
      quiet()
      service.createList("Walk")
      service.setMembership("AAPL", "Walk", true)
      service.switchList("")
      wait(100)

      // The popup first: the window, shown, would take the keys.
      var popupSheet = walk(harness.popup)
      // Whole in both looks: the popup's height takes the sheet's, under its cap.
      var looks = [false, true].map(function(retro) {
        service.persist({ style: retro ? "retro" : "smooth" })
        wait(50)
        return drawnSheet(harness.popup).whole
      })
      harness.check("the key sheet fits the popup without scrolling, smooth and retro", looks[0] && looks[1], looks.join(","))
      panel.close()
      wait(300)

      var windowSheet = walk(harness.window)
      harness.check("the window's sheet is the popup's", popupSheet.all.join("|") === windowSheet.all.join("|"),
        popupSheet.all.join("|") + " vs " + windowSheet.all.join("|"))
      app.close()
      harness.finish()
    }
  }

  Timer {
    interval: 80000
    running: true
    onTriggered: {
      console.log("FAIL Keys harness timed out")
      done.exitCode = 1
      done.start()
    }
  }

  Timer {
    id: done
    property int exitCode: 0
    interval: 100
    onTriggered: Qt.exit(exitCode)
  }
}
