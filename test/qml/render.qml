import QtQuick
import Quickshell
import Quickshell.Io
import qs.Commons
import qs.Ui
import "plugin/Cells.js" as Cells
import "plugin/Tones.js" as Tones
import "plugin/Chart.js" as Chart
import "plugin/Fundamentals.js" as Fundamentals
import "plugin/History.js" as History
import "plugin/Market.js" as Market
import "plugin/Overnight.js" as Overnight
import "plugin/Quote.js" as Quote
import "plugin/Search.js" as Search
import "plugin/Settings.js" as Settings
import "plugin"

// Offscreen window body. A stub surface feeds StonksBody from fixtures.
// Grab the opaque window-sized layer so margins stay in the PNG. The
// w22-<surface>-<look>-<symbol>-<range> states render the 22-symbol list with
// a real day featured, at identical state in both looks; a popup is as tall
// as its whole rows, the way Panel sizes it. sprite-candidates draws the
// bull and bear candidates at 1x and 4x, smooth's drawn pair beside them.
// pill-sheet draws the real bar pill for NBIS in every bar style and look,
// on a horizontal and a vertical bar.
ShellRoot {
  id: root

  readonly property string stateName: Quickshell.env("STONKS_VISUAL_STATE") || "retro"
  readonly property string outPath: Quickshell.env("STONKS_VISUAL_OUT")
  readonly property string fixtureDir: Quickshell.env("STONKS_FIXTURES")
  readonly property string calendarPath: Quickshell.env("STONKS_VISUAL_CALENDARS")
  readonly property var w22: {
    var match = /^w22-(popup|window)-(smooth|retro)-([a-z]+)-(1d|1y|5y)$/.exec(stateName)
    return match ? { popup: match[1] === "popup", symbol: match[3], range: match[4] } : null
  }
  // loading and failed: a cold start, the first quotes out or failed, in
  // retro, or in smooth with -smooth.
  readonly property string coldState: (/^(loading|failed)(?:-smooth)?$/.exec(stateName) || ["", ""])[1]
  // The popup while adding, and the window at the size App.qml permits.
  readonly property bool searchState: stateName.indexOf("-search") >= 0
  readonly property bool minWindow: stateName.indexOf("min-window-") === 0
  // window-1416-<look>: the tiled window at the full 1416 by 850 the live run
  // measured, the 22-symbol list.
  readonly property int windowWidth: {
    var match = /^window-(\d+)-(smooth|retro)$/.exec(stateName)
    return match ? Number(match[1]) : 0
  }
  // <popup|window>-order-<falls|rises>-<look>: the order word both ways.
  readonly property bool orderState: /^(popup|window)-order-(falls|rises)-/.test(stateName)
  readonly property bool bigList: !!w22 || minWindow || windowWidth > 0 || stateName.indexOf("popup-search-") === 0
    || stateName.indexOf("popup-price") === 0
    || stateName.indexOf("popup-waiting-") === 0 || stateName.indexOf("popup-rowstates-") === 0
    || stateName.indexOf("popup-note-") === 0 || stateName.indexOf("popup-lists-") === 0
    || stateName.indexOf("popup-rowlines-") === 0 || stateName.indexOf("popup-52w-") === 0
    || stateName.indexOf("popup-breadth-even-") === 0
    || stateName.indexOf("window-lists-") === 0 || orderState
  readonly property bool sheet: stateName === "sprite-candidates"
  // pill-sheet-large: the same at a larger text size (render.sh).
  readonly property bool pillSheet: stateName === "pill-sheet" || stateName === "pill-sheet-large"
  property var pillQuote: null
  // The same day closed up: the pill's icon form climbs where NBIS's falls.
  readonly property var pillUpQuote: pillQuote ? Object.assign({}, pillQuote, { prevClose: pillQuote.price * 0.97 }) : null
  readonly property bool popup: w22 ? w22.popup : stateName.indexOf("popup") === 0 || themeState
  // <popup-help|popup-help-sorted|window-help>-<look>: the key sheet.
  readonly property var help: {
    var match = /^(popup-help|popup-help-sorted|window-help)-(smooth|retro)$/.exec(stateName)
    return match ? { sorted: match[1] === "popup-help-sorted", window: match[1] === "window-help" } : null
  }
  readonly property bool historyState: stateName.indexOf("history-") === 0
    || stateName.indexOf("popup-history-") === 0
  readonly property bool nbisState: stateName.indexOf("popup-history-1w-") === 0
    || stateName.indexOf("popup-history-1m-") === 0
  readonly property bool nbisDayState: stateName.indexOf("popup-nbis-1d-") === 0
  readonly property bool holidayState: stateName.indexOf("popup-holiday-") === 0
  // popup-overnight-<moment>-<symbol>-<look>: a saved moment from
  // test/fixtures/overnight, NBIS, SNOW, and RVII with Robinhood's prints;
  // at the live moment (Monday 5 October, 00:36), NBIS, ET, and TLN, on 1D
  // or on 1M (-1m), at rest or scrubbed (-1m-scrub); at a dated moment
  // (<date>-<hhmm>, New York), NBIS alone, on 1D at rest or scrubbed to its
  // last print (-scrub). Robinhood trades every one of them all day there.
  // <popup|window>-sweep-<moment>-<symbol>-<look>: the listing sweep's
  // moments (sweep-<date>-<hhmm>), every kind of listing on 1D at rest:
  // NBIS, PSIX, SPY, BLDP, the S&P 500 (gspc), BTC-USD, SHEL.L, and 7203.T,
  // with Robinhood's saved answers on which it trades all day.
  readonly property var overnight: {
    var match = /^popup-overnight-(day|night|sunday|live|\d{4}-\d\d-\d\d-\d{4})-(nbis|snow|rvii|et|tln)(-1m|-1m-scrub|-scrub)?-(smooth|retro)$/.exec(stateName)
    if (match) return { moment: match[1], symbol: match[2].toUpperCase(), month: match[3] === "-1m" || match[3] === "-1m-scrub",
      scrub: !!match[3] && match[3] !== "-1m", dated: /^\d/.test(match[1]), sweep: false }
    match = /^(?:popup|window)-sweep-(\d{4}-\d\d-\d\d-\d{4})-(nbis|psix|spy|bldp|gspc|btc-usd|shel\.l|7203\.t|shib-usd|ry\.to|brk-a|brk-b)-(smooth|retro)$/.exec(stateName)
    return match ? { moment: match[1], symbol: match[2] === "gspc" ? "^GSPC" : match[2].toUpperCase(), month: false, scrub: false,
      dated: true, sweep: true } : null
  }
  // popup-fill-<day>-<look>[-<theme>]: the 1D wash on an up day (SPY 11
  // Sep), a down day (AAPL 4 Sep), and a day across its baseline (NBIS 11
  // Sep). theme-<look>-<theme>: the gallery, the day across its baseline
  // with every pill form beside it. Every render is in an installed Omarchy
  // theme, the one its name ends with or Tokyo Night, which render.sh puts
  // where the shell reads the current theme.
  readonly property bool themeState: /^theme-(smooth|retro)-[a-z0-9-]+$/.test(stateName)
  readonly property var fill: {
    if (themeState) return { day: "cross" }
    var match = /^popup-fill-(up|down|cross)-(smooth|retro)(?:-[a-z0-9-]+)?$/.exec(stateName)
    return match ? { day: match[1] } : null
  }
  // The theme's up and down, as the plugin reads them.
  property TrendColors trendColors: TrendColors {}
  // The surface's ground: the popup's card, opaque, or the window's.
  readonly property color groundColor: popup
    ? Qt.rgba(Color.popups.background.r, Color.popups.background.g, Color.popups.background.b, 1) : Color.background
  // The bar's own ground and ink, opaque, for the pills.
  readonly property color barGround: Qt.rgba(Color.bar.background.r, Color.bar.background.g, Color.bar.background.b, 1)
  property int now: holidayState ? 1813334400 : (nbisState || nbisDayState ? 1789156800 : 1788294000)
  property string shownRange: historyState ? "1Y" : "1D"
  property var shownHistoryEntry: null
  // The saved cap and P/E, with their closes, by symbol: AAPL, NBIS, and
  // SHOP.TO have them; any other symbol shows what its quote alone gives.
  property var savedFigures: ({})

  function figures(name) {
    var read = function(path) {
      var view = Qt.createQmlObject('import Quickshell.Io; FileView { blockLoading: true }', root)
      view.path = fixtureDir + "/fundamentals/" + path + ".json"
      var json = JSON.parse(view.text())
      view.destroy()
      return json
    }
    return Fundamentals.withSnapshotCloses(Fundamentals.parseFundamentals(read(name)), read(name + "-closes"))
  }

  readonly property var bullCandidates: [
    [".##..............##.", "##................##", "##................##", ".###............###.",
     "..#####......#####..", "....############....", "###.############.###", ".##.#.########.#.##.",
     "....############....", "....############....", "...##############...", "...###.######.###...",
     "...##############...", "....############...."],
    Cells.BULL_SPRITE.slice(),
    [".##..............##.", "##................##", "##................##", ".###............###.",
     "..#####......#####..", "....############....", "###.############.###", ".##.#.########.#.##.",
     "....############....", "...##############...", "...###.######.###...", "...##############...",
     "....#####..#####....", "........####........"]
  ]
  readonly property var bearCandidates: [
    [".###............###.", "##.##..........##.##", "##.###.######.###.##", ".##################.",
     "####################", "####.##########.####", "#####.########.#####", "####################",
     "########....########", "#########..#########", "########.##.########", ".##################.",
     "..################..", "....############...."],
    ["..##............##..", ".####..........####.", ".#####.######.#####.", "..################..",
     ".##################.", "####################", "#####.########.#####", "####################",
     "#########..#########", "########.##.########", ".##################.", "..################..",
     "....############....", "......########......"],
    Cells.BEAR_SPRITE.slice(),
    // A front-facing bear: round ears, a heavy head, and a muzzle patch with
    // its nose, so it does not read as a cat.
    ["..###..........###..", ".#####........#####.", ".##.##........##.##.", "..################..",
     ".##################.", "####################", "####.##########.####", "#######......#######",
     "######........######", "######..####..######", "######...##...######", ".#####........#####.",
     "..################..", "....############...."]
  ]

  function isDefaultSprite(rows, standard) {
    return rows.join("|") === standard.join("|")
  }

  FileView { id: aaplFile; path: fixtureDir + "/aapl.json"; blockLoading: true }
  FileView { id: nbisDayFile; path: fixtureDir + "/nbis-2026-09-11-day-prepost.json"; blockLoading: true }
  FileView { id: spyDayFile; path: fixtureDir + "/spy-2026-09-11-day-prepost.json"; blockLoading: true }
  FileView { id: aaplDownFile; path: fixtureDir + "/aapl-2026-09-04-day-prepost.json"; blockLoading: true }
  FileView { id: shopFile; path: fixtureDir + "/shop-to.json"; blockLoading: true }
  FileView { id: searchFile; path: fixtureDir + "/search.json"; blockLoading: true }
  FileView { id: historyFile; path: fixtureDir + "/history/aapl-1y.json"; blockLoading: true }
  FileView { id: nbisWeekFile; path: fixtureDir + "/history/nbis-5d.json"; blockLoading: true }
  FileView { id: nbisMonthFile; path: fixtureDir + "/history/nbis-1mo.json"; blockLoading: true }
  FileView { id: shortFile; path: fixtureDir + "/history/fig-max.json"; blockLoading: true }
  FileView { id: weeklyFile; path: fixtureDir + "/history/aapl-5y.json"; blockLoading: true }
  FileView { id: calendarFile; path: calendarPath; blockLoading: true }
  FileView { id: watchlistFile; path: fixtureDir + "/watchlist-22.json"; blockLoading: true }
  FileView { id: shopYearFile; path: fixtureDir + "/history/shop-to-1y.json"; blockLoading: true }

  QtObject {
    id: stub
    property var symbols: ["AAPL", "SHOP.TO"]
    property bool settingsUnreadable: false
    property var shown: ["AAPL", "SHOP.TO"]
    property var library: symbols
    property string listName: ""
    property var lists: [{ name: "", label: "All", count: symbols.length, symbols: symbols }]
    property var quotes: ({})
    property var entries: ({})
    property var calendars: null
    property string featuredSymbol: "AAPL"
    property string order: "pct"
    property bool reversed: false
    property string changeMode: "pct"
    property int now: root.now
    property var asked: ({})
    property string range: root.shownRange
    readonly property var histories: root.shownHistoryEntry ? ({ [featuredSymbol + "|" + range]: root.shownHistoryEntry }) : ({})
    readonly property string hero: previewSymbol || featuredSymbol
    readonly property var fundamentals: root.savedFigures[hero]
      ? ({ [hero]: { status: "ok", figures: root.savedFigures[hero] } }) : ({})
    property var arriving: []
    property var lastRemoval: null
    // Robinhood's prints, and its word on which listings it trades all day,
    // answered, on the overnight states only.
    property var nights: ({})
    property var allDay: ({})
    readonly property var view: ({ featured: featuredSymbol, range: range, list: listName, listKey: "=" + listName,
      order: order, reversed: reversed, rows: symbols, shown: shown, library: library,
      chart: Chart.viewChart(featuredSymbol, range, quotes, entries, histories, nights, allDay, root.overnight ? calendars : null) })
    // The search's choice on the hero, unadded (popup-search-preview-*).
    property string previewSymbol: ""
    readonly property var previewView: previewSymbol ? Object.assign({}, view, { featured: previewSymbol,
      chart: Chart.viewChart(previewSymbol, range, quotes, entries, histories) }) : view
    readonly property var previewEntries: ({})
    property var previewSurface: null
    function preview(surface, symbol) {
      previewSurface = symbol ? surface : null
      previewSymbol = symbol
    }
    property bool retro: root.stateName !== "smooth" && root.stateName.indexOf("-smooth") < 0
      && root.stateName !== "popup-history-failed" && root.stateName !== "popup-history-loading"
    property color foreground: Color.popups.text
    property color upColor: root.trendColors.up
    property color downColor: root.trendColors.down
    // The shell's own family, so renders measure what the owner sees.
    property string fontFamily: Style.font.family
    function persist(values) {}
    function setRange(value) { root.shownRange = value }
    function surfaceShown(surface, open) {}
    function feature(symbol) { featuredSymbol = symbol }
    function removeSymbol(symbol) {}
    function setManualOrder(symbols) {}
    function addSymbol(symbol) {}
    function setOrder(value) { order = value }
    function switchList(name) {}
    function stepList(delta) {}
    function createList(name) { return "" }
  }
  // The version watch, as a render sets it.
  QtObject {
    id: updates
    property string newVersion: ""
  }

  function load() {
    var aapl = Quote.parseChart(JSON.parse(aaplFile.text()))
    var nbisDay = Quote.parseChart(JSON.parse(nbisDayFile.text()))
    var spyDay = Quote.parseChart(JSON.parse(spyDayFile.text()))
    var shop = Quote.parseChart(JSON.parse(shopFile.text()))
    var history = History.parseHistory(JSON.parse(historyFile.text()), "1Y")
    var nbisJson = JSON.parse(nbisWeekFile.text())
    // A day cut from the week's answer, saved that Friday evening: its
    // current period is that day, its trading periods the week's.
    var nbisCut = JSON.parse(nbisWeekFile.text())
    delete nbisCut.chart.result[0].meta.tradingPeriods
    var nbis = Quote.parseChart(nbisCut)
    nbis.prevClose = nbisJson.chart.result[0].meta.previousClose
    nbis.points = nbis.points.filter(function(point) { return point.t >= nbis.session.pre.start })
    var nbisWeek = History.parseHistory(nbisJson, "1W")
    var nbisMonth = History.parseHistory(JSON.parse(nbisMonthFile.text()), "1M")
    var weekly = History.parseHistory(JSON.parse(weeklyFile.text()), "5Y")
    var shortHistory = History.parseHistory(JSON.parse(shortFile.text()), "All")
    root.savedFigures = { AAPL: root.figures("aapl"), NBIS: root.figures("nbis"), "SHOP.TO": root.figures("shop-to") }
    try { stub.calendars = JSON.parse(calendarFile.text()) } catch (e) { stub.calendars = null }
    if (root.pillSheet) {
      root.pillQuote = nbisDay
      root.now = nbisDay.marketTime + 120
      return
    }
    if (root.coldState === "loading") {
      stub.quotes = {}
      stub.entries = {}
    } else if (root.coldState === "failed") {
      stub.quotes = {}
      stub.entries = { AAPL: { status: "failed" }, "SHOP.TO": { status: "failed" } }
    } else if (root.stateName === "aged") {
      stub.quotes = { AAPL: aapl, "SHOP.TO": shop }
      stub.entries = { AAPL: { status: "ok", receivedAt: root.now - 1000 }, "SHOP.TO": { status: "ok", receivedAt: root.now - 1000 } }
    } else if (root.stateName.indexOf("fetch-saved-") === 0) {
      // Quotes kept from an earlier session, two days on: each says its day.
      root.now = root.now + 2 * 86400
      stub.quotes = { AAPL: aapl, "SHOP.TO": shop }
      stub.entries = { AAPL: { status: "saved", receivedAt: aapl.marketTime }, "SHOP.TO": { status: "saved", receivedAt: shop.marketTime } }
    } else if (root.stateName.indexOf("fetch-overdue-") === 0) {
      // A refresh asked for 91 s ago with no answer: a refusal, or no network.
      stub.quotes = { AAPL: aapl, "SHOP.TO": shop }
      stub.entries = { AAPL: { status: "ok", receivedAt: root.now - 300 }, "SHOP.TO": { status: "ok", receivedAt: root.now - 300 } }
      stub.asked = { AAPL: root.now - 91, "SHOP.TO": root.now - 91 }
    } else {
      stub.quotes = { AAPL: aapl, "SHOP.TO": shop }
      stub.entries = { AAPL: { status: "ok", receivedAt: root.now }, "SHOP.TO": { status: "ok", receivedAt: root.now } }
    }

    if (root.nbisDayState) {
      stub.symbols = ["NBIS", "AAPL"]
      stub.shown = ["NBIS", "AAPL"]
      stub.quotes = { NBIS: nbisDay, AAPL: aapl }
      stub.entries = { NBIS: { status: "ok", receivedAt: root.now }, AAPL: { status: "ok", receivedAt: root.now } }
      stub.featuredSymbol = "NBIS"
    }

    if (root.fill) {
      var days = { up: ["SPY", spyDay], down: ["AAPL", Quote.parseChart(JSON.parse(aaplDownFile.text()))], cross: ["NBIS", nbisDay] }
      var fillQuotes = {}, fillEntries = {}
      ;["up", "down", "cross"].forEach(function(day) {
        fillQuotes[days[day][0]] = days[day][1]
      })
      var featuredDay = days[root.fill.day][1]
      root.now = featuredDay.marketTime + 120
      Object.keys(fillQuotes).forEach(function(s) { fillEntries[s] = { status: "ok", receivedAt: root.now } })
      stub.symbols = ["SPY", "AAPL", "NBIS"]
      stub.shown = stub.symbols.slice()
      stub.quotes = fillQuotes
      stub.entries = fillEntries
      stub.order = "manual"
      stub.featuredSymbol = days[root.fill.day][0]
      if (root.themeState) root.pillQuote = nbisDay
    }

    if (root.overnight) {
      var live = root.overnight.moment === "live"
      var folder = live ? "live-2026-10-05" : (root.overnight.sweep ? "sweep-" : "") + root.overnight.moment
      var saved = function(name) {
        var view = Qt.createQmlObject('import Quickshell.Io; FileView { blockLoading: true; printErrors: false }', root)
        view.path = fixtureDir + "/overnight/" + folder + "/" + name + ".json"
        var text = String(view.text())
        view.destroy()
        return text
      }
      var read = function(name) { return JSON.parse(saved(name)) }
      // The look sweep adds a coin under a cent, Toronto, a six-figure
      // price, and a long symbol at the moments that saved them (8 October,
      // and Saturday 3 October), listing the featured one first so its row
      // shows.
      var file = function(s) { return s.toLowerCase().replace(/^\^/, "") }
      var looked = root.overnight.sweep && saved(file("SHIB-USD")) !== ""
      var listed = looked ? ["NBIS", "PSIX", "SPY", "BLDP", "^GSPC", "BTC-USD", "SHIB-USD", "SHEL.L", "7203.T", "RY.TO", "BRK-A", "BRK-B"]
        : root.overnight.sweep ? ["NBIS", "PSIX", "SPY", "BLDP", "^GSPC", "BTC-USD", "SHEL.L", "7203.T"]
        : live ? ["NBIS", "ET", "TLN"] : root.overnight.dated ? ["NBIS"] : ["NBIS", "SNOW", "RVII"]
      if (looked) listed = [root.overnight.symbol].concat(listed.filter(function(s) { return s !== root.overnight.symbol }))
      // The US listings: Robinhood's word on which it trades all day, saved
      // with the sweep's moments, and every one at the moments before them.
      var us = looked ? ["NBIS", "PSIX", "SPY", "BLDP", "BRK-A", "BRK-B"]
        : root.overnight.sweep ? ["NBIS", "PSIX", "SPY", "BLDP"] : listed
      var tradable = root.overnight.sweep ? Overnight.parseAllDay(read("robinhood-instruments"), us) : null
      // What a surface opening at the moment holds: Robinhood's span=day
      // answer.
      var answer = Overnight.parseOvernight(read("robinhood-day"), us)
      var quotes = {}, entries = {}, nights = {}, allDay = {}
      listed.forEach(function(s) {
        quotes[s] = Quote.parseChart(read(file(s)))
      })
      us.forEach(function(s) {
        allDay[s] = { status: "ok", allDay: tradable ? tradable[s] : true, at: 0 }
        if (allDay[s].allDay) nights[s] = { status: "ok", bars: answer[s] }
      })
      // The moment each was saved at: Friday 2 October 13:42, Thursday
      // 1 October 23:00, Sunday 27 September 22:30, Monday 5 October 00:36;
      // a dated one at its name, New York time.
      var dated = root.overnight.dated ? /^(.{10})-(\d\d)(\d\d)$/.exec(root.overnight.moment) : null
      root.now = dated ? Market.epochAt(Overnight.overnightMarket(stub.calendars), dated[1], dated[2] + ":" + dated[3])
        : { day: 1790962920, night: 1790910000, sunday: 1790562600, live: 1791175013 }[root.overnight.moment]
      listed.forEach(function(s) { entries[s] = { status: "ok", receivedAt: root.now } })
      if (root.overnight.month) {
        root.shownRange = "1M"
        root.shownHistoryEntry = { status: "ok", history: History.parseHistory(read(root.overnight.symbol.toLowerCase() + "-1m"), "1M"), receivedAt: root.now }
      }
      stub.symbols = listed
      stub.shown = listed.slice()
      stub.quotes = quotes
      stub.entries = entries
      stub.nights = nights
      stub.allDay = allDay
      stub.order = "manual"
      stub.featuredSymbol = root.overnight.symbol
      return
    }

    if (root.bigList) {
      var fixture = JSON.parse(watchlistFile.text())
      // popup-52w-<look>: MU's day reaches its 52-week high.
      if (root.stateName.indexOf("popup-52w-") === 0) {
        fixture.quotes.forEach(function(q) {
          var meta = q.response ? q.response.chart.result[0].meta : null
          if (meta && meta.symbol === "MU") meta.fiftyTwoWeekHigh = meta.regularMarketDayHigh
        })
      }
      var names = []
      var loaded = {}
      var states = {}
      for (var n = 0; n < fixture.quotes.length; n++) {
        var entry = fixture.quotes[n]
        var chart = entry.response ? Quote.parseChart(entry.response) : null
        var name = chart ? chart.symbol : entry.symbol
        names.push(name)
        states[name] = entry.status
        if (chart) loaded[name] = chart
      }
      // The featured symbols carry a real day, pre- and post-market included.
      loaded.AAPL = aapl
      loaded.SPY = spyDay
      loaded["SHOP.TO"] = shop
      var featuredName = root.w22 ? { aapl: "AAPL", spy: "SPY", shop: "SHOP.TO", mu: "MU" }[root.w22.symbol]
        : (root.stateName.indexOf("popup-price5-") === 0 ? "BRK-A" : "MU")
      // popup-price4-solo-<look>: the day without its post-market prints, so
      // the strip under the price is empty.
      if (root.stateName.indexOf("-solo-") >= 0) {
        var day = loaded[featuredName]
        loaded[featuredName] = Object.assign({}, day, {
          points: day.points.filter(function(point) { return point.t < day.session.regular.end })
        })
      }
      root.now = loaded[featuredName].marketTime + 120
      var fresh = {}
      for (var e = 0; e < names.length; e++)
        fresh[names[e]] = { status: states[names[e]], receivedAt: states[names[e]] === "ok" ? root.now : 0 }
      stub.symbols = names
      stub.shown = names.slice()
      stub.quotes = loaded
      stub.entries = fresh
      stub.order = "manual"
      stub.featuredSymbol = featuredName
      // popup-rowlines-<look>: rows whose day crosses its previous close
      // (AAPL's pre-market under it, SHOP.TO over then under), for the row
      // line's two colours.
      if (root.stateName.indexOf("popup-rowlines-") === 0) {
        stub.symbols = ["AAPL", "SHOP.TO", "BRK-A", "TSLA", "SPY", "MU"]
        stub.shown = stub.symbols.slice()
      }
      // popup-breadth-even-<look>: a list three up and three down, whose two
      // ends of the rule must be the same length and never meet.
      if (root.stateName.indexOf("popup-breadth-even-") === 0) {
        stub.library = stub.symbols.slice()
        stub.listName = "Even"
        stub.symbols = ["MU", "BRK-A", "LONG", "MSFT", "TSLA", "AMZN"]
        stub.shown = stub.symbols.slice()
      }
      // The order word both ways: All by % change, the biggest gain first
      // and, reversed, the biggest loss first.
      if (root.orderState) {
        stub.order = "pct"
        stub.reversed = root.stateName.indexOf("-rises-") > 0
        stub.shown = Settings.sortedSymbols(stub.symbols, stub.quotes, stub.order, stub.reversed)
      }
      if (root.w22 && root.w22.range === "5y") {
        root.shownRange = "5Y"
        root.shownHistoryEntry = { status: "ok", history: weekly, receivedAt: root.now }
      } else if (root.w22 && root.w22.range === "1y") {
        root.shownRange = "1Y"
        root.shownHistoryEntry = { status: "ok", history: History.parseHistory(JSON.parse(shopYearFile.text()), "1Y"), receivedAt: root.now }
      }
      return
    }

    if (root.historyState) {
      // popup-history-1w-* and -1m-*: NBIS on 15-minute and hourly bars, the
      // only ranges drawn from intraday history.
      if (root.nbisState) {
        root.shownRange = root.stateName.indexOf("popup-history-1w-") === 0 ? "1W" : "1M"
        root.shownHistoryEntry = { status: "ok", history: root.shownRange === "1W" ? nbisWeek : nbisMonth, receivedAt: root.now }
        stub.symbols = ["NBIS", "AAPL"]
        stub.shown = ["NBIS", "AAPL"]
        stub.quotes = { NBIS: nbis, AAPL: aapl }
        stub.entries = { NBIS: { status: "ok", receivedAt: root.now }, AAPL: { status: "ok", receivedAt: root.now } }
        stub.featuredSymbol = "NBIS"
      } else if (root.stateName.indexOf("popup-history-all-") === 0) {
        root.shownRange = "All"
        root.shownHistoryEntry = { status: "ok", history: shortHistory, receivedAt: root.now }
        var fig = Object.assign({}, aapl, { symbol: "FIG", name: "Figma", price: 23.20, prevClose: 23.20 })
        stub.symbols = ["FIG", "AAPL"]
        stub.shown = ["FIG", "AAPL"]
        stub.quotes = { FIG: fig, AAPL: aapl }
        stub.entries = { FIG: { status: "ok", receivedAt: root.now }, AAPL: { status: "ok", receivedAt: root.now } }
        stub.featuredSymbol = "FIG"
      } else if (root.stateName === "history-sparse") {
        root.shownRange = "All"
        root.shownHistoryEntry = { status: "ok", receivedAt: root.now, history: {
          symbol: "AAPL", range: "All", interval: "1mo", currency: "USD", gmtoffset: -14400,
          baseline: 95, firstTradeDate: history.bars[0].t, bars: history.bars.slice(0, 5)
        } }
      } else if (root.stateName === "popup-history-loading") {
        // A refetch out with the range in hand: the last answer, aged.
        root.shownHistoryEntry = { status: "ok", history: history, receivedAt: root.now - 900 }
      } else if (root.stateName === "history-empty-loading") {
        // No answer yet.
        root.shownHistoryEntry = null
      } else if (root.stateName === "popup-history-failed") {
        root.shownHistoryEntry = { status: "failed", history: history, receivedAt: root.now - 900 }
      } else {
        root.shownHistoryEntry = { status: "ok", history: history, receivedAt: root.now }
      }
    }
  }

  Component.onCompleted: {
    load()
    if (root.searchState) {
      body.adding = true
      body.search.results = Search.parseSearch(JSON.parse(searchFile.text()))
      body.search.searchText = "SHOP"
      body.search.resultsQuery = "SHOP"
      body.search.answer = "results"
      // popup-search-stale-*: SHOP's results under a newer query, dimmed;
      // -none-: an empty answer; -failed-: no answer at all.
      if (root.stateName.indexOf("-stale-") > 0) body.search.searchText = "SHOPI"
      // popup-search-preview-*: SHOP.TO, not in the list, chosen with the
      // arrows, so the hero shows it unadded beside the Add on its row.
      if (root.stateName.indexOf("-preview-") > 0) {
        stub.symbols = stub.symbols.filter(function(s) { return s !== "SHOP.TO" })
        stub.shown = stub.symbols.slice()
        root.now = stub.quotes["SHOP.TO"].marketTime + 120
        stub.entries = Object.assign({}, stub.entries, { "SHOP.TO": { status: "ok", receivedAt: root.now } })
        body.search.showResult(1)
      }
      // popup-search-show-*: SHOP.TO, already in the list, chosen: its
      // button and the field's hint say Show, in Add's slot.
      if (root.stateName.indexOf("-show-") > 0) {
        root.now = stub.quotes["SHOP.TO"].marketTime + 120
        stub.entries = Object.assign({}, stub.entries, { "SHOP.TO": { status: "ok", receivedAt: root.now } })
        body.search.showResult(1)
      }
      if (root.stateName.indexOf("-none-") > 0 || root.stateName.indexOf("-failed-") > 0) {
        body.search.results = []
        body.search.resultsQuery = "ZZZ"
        body.search.searchText = "ZZZ"
        body.search.answer = root.stateName.indexOf("-none-") > 0 ? "none" : "failed"
      }
    } else if (root.help) {
      // Manual order, so the sheet shows every entry it can; popup-help-sorted
      // shows a sorted order's instead, without J K and the drag.
      stub.order = root.help.sorted ? "pct" : "manual"
      body.showingHelp = true
    } else if (root.stateName.indexOf("popup-waiting-") === 0) {
      // Search open, its first answer still out: the rows stay.
      body.adding = true
    } else if (root.stateName.indexOf("popup-rowstates-") === 0) {
      // Featured, cursor, and a row that has not moved, side by side.
      body.watchlist.cursorSymbol = stub.shown[2]
      var still = stub.quotes["SHOP.TO"]
      var quotes = Object.assign({}, stub.quotes)
      quotes["SHOP.TO"] = Object.assign({}, still, { prevClose: Quote.regularClose(still) })
      stub.quotes = quotes
    } else if (root.stateName.indexOf("popup-note-") === 0) {
      body.note = "Keep at least one symbol"
    } else if (root.stateName.indexOf("popup-hint-") === 0) {
      // The first open's footer: the line has to fit the popup's width.
      body.showFirstRunHint()
    } else if (root.stateName.indexOf("popup-notice-file-") === 0) {
      // The footer's notices have to fit the popup's width.
      stub.settingsUnreadable = true
    } else if (root.stateName.indexOf("popup-notice-update-") === 0) {
      updates.newVersion = "1.0.0"
    } else if (root.stateName.indexOf("popup-lists-") === 0 || root.stateName.indexOf("window-lists-") === 0) {
      // popup-lists-<menu|named|empty|long|manage|rename|confirm|symbol>-<look>
      // and window-lists-<manage|symbol>-<look>: named watchlists over the
      // 22-symbol library, the featured symbol staying put on a switch.
      var all = stub.symbols.slice()
      var named = root.stateName.indexOf("-named-") > 0 ? { name: "My Portfolio", symbols: all.slice(0, 8) }
        : root.stateName.indexOf("-empty-") > 0 ? { name: "Energy", symbols: [] }
        : root.stateName.indexOf("-long-") > 0 ? { name: "Semiconductors & AI infrastructure", symbols: all.slice(3, 8) }
        : null
      var choice = function(name, symbols) { return { name: name, label: name || "All", count: symbols.length, symbols: symbols } }
      stub.lists = [choice("", all), choice("My Portfolio", all.slice(0, 8)), choice("Studying", all.slice(3, 8)),
        choice("Energy", []), choice("Wins", all.slice(10, 14)), choice("Losses", all.slice(14, 17))]
      if (named) {
        // All stays the library, so the popup keeps All's height on a list.
        stub.library = all
        stub.listName = named.name
        stub.order = "manual"
        stub.symbols = named.symbols
        stub.shown = named.symbols.slice()
      }
      var child = function(name) { return body.children.filter(function(c) { return c.objectName === name })[0] }
      var find = function(item, name) {
        if (item.objectName === name) return item
        for (var i = 0; i < item.children.length; i++) {
          var found = find(item.children[i], name)
          if (found) return found
        }
        return null
      }
      if (root.stateName.indexOf("-menu-") > 0) {
        body.openListMenu()
      } else if (root.stateName.indexOf("-symbol-") > 0) {
        body.openSymbolLists(body.watchlist.cursorRow)
      } else if (/-(manage|rename|confirm)-/.test(root.stateName)) {
        body.openManageLists()
        // After the view opens on its first named list.
        Qt.callLater(function() {
          var manage = child("manageLists")
          if (root.stateName.indexOf("-confirm-") > 0) manage.askDelete(4)
          if (root.stateName.indexOf("-rename-") > 0) {
            manage.startRenaming(2)
            Qt.callLater(function() {
              find(manage, "renameField").text = "wins"
              manage.problem = "wins already exists"
            })
          }
        })
      }
    } else if (root.stateName === "history-scrub") {
      body.motion.scrubT = root.shownHistoryEntry.history.bars[125].t
    } else if (root.overnight && root.overnight.scrub && root.overnight.month) {
      var bars = root.shownHistoryEntry.history.bars
      body.motion.scrubT = bars[Math.floor(bars.length / 2)].t
    } else if (root.overnight && root.overnight.scrub) {
      var day = body.featuredDay.points
      body.motion.scrubT = day[day.length - 1].t
    }
  }

  FloatingWindow {
    visible: true
    color: root.groundColor
    implicitWidth: root.pillSheet ? Math.max(520, pillColumn.implicitWidth + 48) : root.themeState ? 480 + 410 : root.sheet ? 1000
      : (root.windowWidth ? root.windowWidth : (root.minWindow ? 560 : (root.popup ? 480 : 720)))
    // The larger text size lands after the window has taken its height, so
    // the large sheet is given room for its seven rows.
    implicitHeight: root.stateName === "pill-sheet-large" ? 580 : root.pillSheet ? pillColumn.implicitHeight + 40 : root.sheet ? 460
      : root.themeState ? Math.max(body.fittedHeight(body.chromeHeight + 3 * body.rowPitch - body.rowGap), pillColumn.implicitHeight + 40)
      : (root.windowWidth ? 850
        : (root.minWindow ? body.chromeHeight + body.listRowHeight
          : (root.popup ? body.fittedHeight(body.chromeHeight + 6 * body.rowPitch - body.rowGap)
            : (root.help && root.help.window ? 520 : 760))))

    Rectangle {
      id: frame
      anchors.fill: parent
      color: root.groundColor

      StonksBody {
        id: body
        visible: !root.sheet && !root.pillSheet
        // As App.qml draws it: past 720 the body keeps that width, centred.
        // The gallery's popup is at the left, the pills beside it.
        width: root.themeState ? 480 : Math.min(parent.width, Style.space(720))
        height: root.themeState ? body.fittedHeight(body.chromeHeight + 3 * body.rowPitch - body.rowGap) : parent.height
        x: root.themeState ? 0 : (parent.width - width) / 2
        service: stub
        updates: updates
        foreground: stub.foreground
        upColor: stub.upColor
        downColor: stub.downColor
        fontFamily: stub.fontFamily
        surfaceOpen: true
        headerWhenMissing: root.coldState === "loading" ? "Loading" : "No quote"
        failClosedHeader: root.coldState === "failed"
        margins: Style.space(16)
        chartHeight: root.popup ? Style.space(190) : Style.space(220)
        ground: root.groundColor
      }

      Column {
        id: pillColumn
        visible: root.pillSheet || root.themeState
        x: root.themeState ? 480 + 8 : 24
        y: 20
        spacing: 8

        Repeater {
          // Every form in each look, the icon on a down day and an up one,
          // then the pill in the right section with no form picked there,
          // where it is its icon, down and up.
          model: {
            if (root.themeState) {
              var retroLook = root.stateName.indexOf("theme-retro-") === 0
              return ["sparkline", "arrow", "text", "icon"].map(function(style) { return { style: style, retro: retroLook, compact: true } })
                .concat([{ style: "text", retro: retroLook, up: true, compact: true }, { style: "icon", retro: retroLook, up: true, compact: true }])
            }
            if (!root.pillSheet) return []
            var rows = []
            for (var look = 0; look < 2; look++) {
              var retro = look === 1
              ;["sparkline", "arrow", "text", "icon"].forEach(function(style) { rows.push({ style: style, retro: retro }) })
              rows.push({ style: "icon", retro: retro, up: true })
            }
            var right = [{ style: "sparkline", retro: false, right: true }, { style: "sparkline", retro: true, right: true },
              { style: "sparkline", retro: false, right: true, up: true }, { style: "sparkline", retro: true, right: true, up: true }]
            // At the larger size, the smooth look's rows fill the screen.
            return root.stateName === "pill-sheet-large" ? rows.filter(function(r) { return !r.retro })
              .concat(right.filter(function(r) { return !r.retro })) : rows.concat(right)
          }

          Row {
            id: pillRow
            required property var modelData
            spacing: 16

            Text {
              width: pillRow.modelData.compact ? 80 : 170
              anchors.verticalCenter: parent.verticalCenter
              text: (pillRow.modelData.compact ? "" : (pillRow.modelData.retro ? "RETRO" : "SMOOTH") + " · ") + (pillRow.modelData.right ? "RIGHT SECTION" : pillRow.modelData.style.toUpperCase())
                + (pillRow.modelData.up ? " · UP" : pillRow.modelData.style === "icon" || pillRow.modelData.right ? " · DOWN" : "")
              color: Color.muted
              font.family: Style.font.family
              font.pixelSize: 10
              font.bold: true
            }

            // A strip of bar at the bar's height, the real pill on it, fed
            // by a service stub of its own.
            Rectangle {
              id: pillCell
              width: 230
              height: Style.bar.sizeHorizontal
              anchors.verticalCenter: parent.verticalCenter
              color: root.barGround

              QtObject {
                id: pillService
                property string featuredSymbol: "NBIS"
                property var quotes: ({ NBIS: pillRow.modelData.up ? root.pillUpQuote : root.pillQuote })
                property var entries: ({ NBIS: { status: "ok", receivedAt: root.now } })
                property string changeMode: "pct"
                property bool retro: pillRow.modelData.retro
                property int now: root.now
                property var asked: ({})
              }
              QtObject {
                id: pillShell
                function serviceFor() { return { service: pillService, trendColors: root.trendColors } }
              }
              PluginBarApi {
                id: pillApi
                pluginId: "grvc.stonks"
                moduleName: "grvc.stonks"
                shell: pillShell
                // The bar mirrors these onto the facade; the render sets them.
                foreground: Color.bar.text
                barForeground: Color.bar.text
                fontFamily: Style.font.family
                barSize: Style.bar.sizeHorizontal
                layoutConfig: pillRow.modelData.right ? { left: [], center: [], right: [{ id: "grvc.stonks" }] } : ({})
              }
              BarWidget {
                anchors.verticalCenter: parent.verticalCenter
                x: pillRow.modelData.right ? parent.width - width - 8 : 0
                width: implicitWidth
                height: parent.height
                bar: pillApi
                settings: ({ id: "grvc.stonks", barStyle: pillRow.modelData.style })
              }
              // An Omarchy bar icon beside it, Bluetooth's, as the right
              // section draws its icons: what the mark is sized against.
              BarIconButton {
                visible: pillRow.modelData.right === true
                anchors.verticalCenter: parent.verticalCenter
                x: parent.width - 8 - 2 * Style.bar.iconSlot
                height: parent.height
                bar: pillApi
                text: "\uDB80\uDCAF"
              }
            }

            // The same pill on a strip of vertical bar.
            Rectangle {
              width: Style.bar.sizeVertical
              height: Style.bar.iconSlot * 2
              anchors.verticalCenter: parent.verticalCenter
              color: root.barGround

              PluginBarApi {
                id: verticalApi
                pluginId: "grvc.stonks"
                moduleName: "grvc.stonks"
                shell: pillShell
                vertical: true
                foreground: Color.bar.text
                barForeground: Color.bar.text
                fontFamily: Style.font.family
                barSize: Style.bar.sizeVertical
                layoutConfig: pillApi.layoutConfig
              }
              BarWidget {
                anchors.centerIn: parent
                width: parent.width
                height: implicitHeight
                bar: verticalApi
                settings: ({ id: "grvc.stonks", barStyle: pillRow.modelData.style })
              }
            }
          }
        }
      }

      Column {
        visible: root.sheet
        x: 24
        y: 20
        spacing: 16

        Repeater {
          model: [
            { title: "BULL", color: "#8fc08a", sprites: root.bullCandidates, standard: Cells.BULL_SPRITE,
              wash: Tones.washAlpha("#8fc08a", "#8fc08a", root.groundColor, 0.16, 1) },
            { title: "BEAR", color: "#e05a7a", sprites: root.bearCandidates, standard: Cells.BEAR_SPRITE,
              wash: Tones.washAlpha("#e05a7a", "#8fc08a", root.groundColor, 0.16, 1) }
          ]

          Column {
            id: family
            required property var modelData
            spacing: 8

            Row {
              spacing: 32
              Repeater {
                model: family.modelData.sprites
                Column {
                  id: candidate
                  required property var modelData
                  required property int index
                  spacing: 6
                  Text {
                    text: family.modelData.title + " " + (candidate.index + 1)
                      + (root.isDefaultSprite(candidate.modelData, family.modelData.standard) ? "  DEFAULT" : "")
                    color: "#8a8d8d"
                    font.family: Style.font.family
                    font.pixelSize: 10
                    font.bold: true
                  }
                  Row {
                    spacing: 16
                    PixelSprite { width: 40; height: 28; pixels: Cells.spritePixels(candidate.modelData); color: family.modelData.color }
                    PixelSprite { width: 80; height: 56; pixels: Cells.spritePixels(candidate.modelData); color: family.modelData.color }
                  }
                }
              }
              // Smooth's drawn animal, in the theme's wash as the header lays it.
              Column {
                spacing: 6
                Text {
                  text: family.modelData.title + "  SMOOTH"
                  color: "#8a8d8d"
                  font.family: Style.font.family
                  font.pixelSize: 10
                  font.bold: true
                }
                Row {
                  spacing: 16
                  DrawnAnimal { width: 40; height: 28; bull: family.modelData.title === "BULL"; color: family.modelData.color; wash: family.modelData.wash }
                  DrawnAnimal { width: 80; height: 56; bull: family.modelData.title === "BULL"; color: family.modelData.color; wash: family.modelData.wash }
                }
              }
            }

            Row {
              spacing: 32
              Repeater {
                model: family.modelData.sprites
                PixelSprite {
                  required property var modelData
                  width: 160
                  height: 112
                  pixels: Cells.spritePixels(modelData)
                  color: family.modelData.color
                }
              }
              DrawnAnimal {
                width: 160
                height: 112
                bull: family.modelData.title === "BULL"
                color: family.modelData.color
                wash: family.modelData.wash
              }
            }
          }
        }
      }
    }

    Timer {
      interval: 300
      running: true
      onTriggered: frame.grabToImage(function(result) {
        result.saveToFile(root.outPath)
        exitTimer.start()
      })
    }
  }

  Timer {
    id: exitTimer
    interval: 1
    onTriggered: Qt.exit(0)
  }
}
